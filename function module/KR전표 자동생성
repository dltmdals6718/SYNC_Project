FUNCTION Z_FCFI_DOC_1.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(IS_INVOICE_HEADER) TYPE  ZTFC_FIT070
*"     REFERENCE(IT_INVOCIE_ITEM) TYPE  ZIFC_FI_0010
*"  EXCEPTIONS
*"      INVALID_INVOICE_HEADER
*"      INVALID_INVOICE_ITEM
*"      ERROR_DATABASE
*"----------------------------------------------------------------------

  DATA: LS_DOCUMENT_HEADER TYPE ZTFC_FIT020, " 생성을 위한 전표 헤더
        LS_INVOICE_ITEM    LIKE LINE OF IT_INVOCIE_ITEM, " 송장 아이템
        LT_DOCUMENT_ITEM   TYPE TABLE OF ZTFC_FIT030, " 전표 아이템
        LS_DOCUMENT_ITEM   LIKE LINE OF LT_DOCUMENT_ITEM. " 전표 아이템

  DATA: LS_SUBLEDGER  TYPE ZTFC_FIT050. " 보조원장

  DATA: LV_EMPLOYEE        TYPE ZTFC_SDT040,
        LV_DOCUMENT_NUMBER TYPE CHAR10.

* 상품, 소모품 총 금액, 단가 * 수량 금액
  DATA: LV_GOODS_AMOUNT      TYPE ZTFC_FIT030-WRBTR,
        LV_CONSUMABLE_AMOUNT TYPE ZTFC_FIT030-WRBTR,
        LV_AMOUNT            TYPE ZTFC_FIT030-WRBTR.

* 송장 헤더 존재하는가?
  IF IS_INVOICE_HEADER IS INITIAL.
    RAISE INVALID_INVOICE_HEADER.
  ENDIF.

* 송장 아이템 존재하는가?
  IF LINES( IT_INVOCIE_ITEM ) = 0.
    RAISE INVALID_INVOICE_ITEM.
  ENDIF.

* 생성자 사원 정보 조회
  SELECT SINGLE *
    FROM ZTFC_SDT040
    INTO LV_EMPLOYEE
    WHERE ID = SY-UNAME.

* 전표 번호
  CALL FUNCTION 'NUMBER_GET_NEXT'
    EXPORTING
      NR_RANGE_NR = '01'
      OBJECT      = 'ZFC_FI_01'
      TOYEAR      = SY-DATUM+0(4)
    IMPORTING
      NUMBER      = LV_DOCUMENT_NUMBER.
  IF SY-SUBRC <> 0.
* Implement suitable error handling here
  ENDIF.

* KR 전표 해더
  LS_DOCUMENT_HEADER-BELNR = LV_DOCUMENT_NUMBER. " 전표 번호
  LS_DOCUMENT_HEADER-INVNR = IS_INVOICE_HEADER-INVNR. " 송장 번호
  LS_DOCUMENT_HEADER-PERNR = LV_EMPLOYEE-PERNR. " 사원 코드
  LS_DOCUMENT_HEADER-BKSTAT = '1'. " 승인 상태
  LS_DOCUMENT_HEADER-GJAHR = SY-DATUM+0(4). " 회계연도
  LS_DOCUMENT_HEADER-BLART = 'KR'. " 공급업체 매입 전표
  LS_DOCUMENT_HEADER-BUDAT = SY-DATUM. " 전기일자
  LS_DOCUMENT_HEADER-BLDAT = IS_INVOICE_HEADER-INVDAT. " 증빙 일자 = 송장 발급일자
  LS_DOCUMENT_HEADER-CRESTAT = IS_INVOICE_HEADER-ZLSCH. " 신용거래 발생 여부
  LS_DOCUMENT_HEADER-WAERS = IS_INVOICE_HEADER-WAERS. " 통화
  LS_DOCUMENT_HEADER-BKTXT = IS_INVOICE_HEADER-BKTXT. " 적요

* 상품(자산), 소모품(박스) 총 금액 계산
  LOOP AT IT_INVOCIE_ITEM INTO LS_INVOICE_ITEM.

    IF LS_INVOICE_ITEM-MATNR CP 'PKBOX*'. " 박스 품목
      LV_AMOUNT = LS_INVOICE_ITEM-PRICE * LS_INVOICE_ITEM-MENGE.
      ADD LV_AMOUNT TO LV_CONSUMABLE_AMOUNT. " 소모품 총 금액 갱신
    ELSE. " 상품
      LV_AMOUNT = LS_INVOICE_ITEM-PRICE * LS_INVOICE_ITEM-MENGE.
      ADD LV_AMOUNT TO LV_GOODS_AMOUNT. " 상품 총 금액 갱신
    ENDIF.

  ENDLOOP.

* 소모품 구입 했다면
  IF LV_CONSUMABLE_AMOUNT IS NOT INITIAL.
    CLEAR LS_DOCUMENT_ITEM.
    LS_DOCUMENT_ITEM-BELNR = LV_DOCUMENT_NUMBER. " 전표 번호
    LS_DOCUMENT_ITEM-LIFNR = IS_INVOICE_HEADER-LIFNR. " 공급업체 코드
    LS_DOCUMENT_ITEM-SKANR = 1040000002. " 소모품 계정과목
    LS_DOCUMENT_ITEM-BSCHL = 40. " 전기키
    LS_DOCUMENT_ITEM-KOART = 'S'. " 계정유형
    LS_DOCUMENT_ITEM-WRBTR = LV_CONSUMABLE_AMOUNT.
    LS_DOCUMENT_ITEM-WAERS = IS_INVOICE_HEADER-WAERS.
    LS_DOCUMENT_ITEM-SHKZG = 'S'. " 차대변 구분
    LS_DOCUMENT_ITEM-SGTXT = '소모품 박스'.
    APPEND LS_DOCUMENT_ITEM TO LT_DOCUMENT_ITEM.
  ENDIF.

* 상품 구입 했다면
  IF LV_GOODS_AMOUNT IS NOT INITIAL.
    CLEAR LS_DOCUMENT_ITEM.
    LS_DOCUMENT_ITEM-BELNR = LV_DOCUMENT_NUMBER.
    LS_DOCUMENT_ITEM-LIFNR = IS_INVOICE_HEADER-LIFNR.
    LS_DOCUMENT_ITEM-SKANR = 1040000001. " 상품
    LS_DOCUMENT_ITEM-BSCHL = 40.
    LS_DOCUMENT_ITEM-KOART = 'S'.
    LS_DOCUMENT_ITEM-WRBTR = LV_GOODS_AMOUNT.
    LS_DOCUMENT_ITEM-WAERS = IS_INVOICE_HEADER-WAERS.
    LS_DOCUMENT_ITEM-SHKZG = 'S'.
    LS_DOCUMENT_ITEM-SGTXT = '상품 구매'.
    APPEND LS_DOCUMENT_ITEM TO LT_DOCUMENT_ITEM.
  ENDIF.

* 외상인지 현금인지
  CLEAR LS_DOCUMENT_ITEM.
  CASE IS_INVOICE_HEADER-ZLSCH.
    WHEN '0'. " 현금
      LS_DOCUMENT_ITEM-BELNR = LV_DOCUMENT_NUMBER.
      LS_DOCUMENT_ITEM-LIFNR = IS_INVOICE_HEADER-LIFNR.
      LS_DOCUMENT_ITEM-SKANR = 1010000002. " (보통예금)
      LS_DOCUMENT_ITEM-BSCHL = 50.
      LS_DOCUMENT_ITEM-KOART = 'S'.
      LS_DOCUMENT_ITEM-WRBTR = IS_INVOICE_HEADER-WRBTR.
      LS_DOCUMENT_ITEM-WAERS = IS_INVOICE_HEADER-WAERS.
      LS_DOCUMENT_ITEM-SHKZG = 'H'.
      LS_DOCUMENT_ITEM-SGTXT = '보통예금'.
      APPEND LS_DOCUMENT_ITEM TO LT_DOCUMENT_ITEM.
    WHEN '1'. " 외상
      LS_DOCUMENT_HEADER-BALANCE = IS_INVOICE_HEADER-WRBTR. " 전표 헤더에 외상 잔액 기입
      LS_DOCUMENT_ITEM-BELNR = LV_DOCUMENT_NUMBER.
      LS_DOCUMENT_ITEM-LIFNR = IS_INVOICE_HEADER-LIFNR.
      LS_DOCUMENT_ITEM-SKANR = 2000000001.
      LS_DOCUMENT_ITEM-BSCHL = 31.
      LS_DOCUMENT_ITEM-KOART = 'K'.
      LS_DOCUMENT_ITEM-WRBTR = IS_INVOICE_HEADER-WRBTR.
      LS_DOCUMENT_ITEM-WAERS = IS_INVOICE_HEADER-WAERS.
      LS_DOCUMENT_ITEM-SHKZG = 'H'.
      LS_DOCUMENT_ITEM-SGTXT = '외상매입금'.
      APPEND LS_DOCUMENT_ITEM TO LT_DOCUMENT_ITEM.

*     보조원장 기입
      LS_SUBLEDGER-GJAHR = SY-DATUM+0(4). " 회계연도
      LS_SUBLEDGER-KOART = 'K'. " 계정유형 Vender
      LS_SUBLEDGER-LIFNR = IS_INVOICE_HEADER-LIFNR. " 공급업체 코드
      LS_SUBLEDGER-BELNR = LV_DOCUMENT_NUMBER. " 전표 번호
      LS_SUBLEDGER-BSCHL = 31. " 전기키
      LS_SUBLEDGER-SHKZG = 'H'. " 대변
      LS_SUBLEDGER-BUDAT = SY-DATUM. " 전기일
      LS_SUBLEDGER-WRBTR = IS_INVOICE_HEADER-WRBTR. " 금액
      LS_SUBLEDGER-WAERS = IS_INVOICE_HEADER-WAERS. " 통화
      LS_SUBLEDGER-BALANCE = IS_INVOICE_HEADER-WRBTR. " 잔액

      CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL' " 지급기한
        EXPORTING
          DATE      = IS_INVOICE_HEADER-INVDAT
          DAYS      = 0
          MONTHS    = 3
          SIGNUM    = '+'
          YEARS     = 0
        IMPORTING
          CALC_DATE = LS_SUBLEDGER-PAYDUEDAT.

      INSERT ZTFC_FIT050 FROM LS_SUBLEDGER.
      IF SY-SUBRC <> 0.
        RAISE ERROR_DATABASE.
        ROLLBACK WORK.
        RETURN.
      ENDIF.


  ENDCASE.

* 전표 헤더 INSERT DB 과정
  INSERT ZTFC_FIT020 FROM LS_DOCUMENT_HEADER.

* 전표 아이템 번호 라벨링
  LOOP AT LT_DOCUMENT_ITEM INTO LS_DOCUMENT_ITEM.
    LS_DOCUMENT_ITEM-BUZEI = SY-TABIX.
    INSERT ZTFC_FIT030 FROM LS_DOCUMENT_ITEM.

    IF SY-SUBRC <> 0.
      RAISE ERROR_DATABASE.
      ROLLBACK WORK.
      RETURN.
    ENDIF.

  ENDLOOP.

* 송장에 전표 번호 넣어주기
  UPDATE ZTFC_FIT070
    SET BELNR = LV_DOCUMENT_NUMBER
    WHERE INVNR = IS_INVOICE_HEADER-INVNR.

  IF SY-SUBRC <> 0.
    RAISE ERROR_DATABASE.
    ROLLBACK WORK.
    RETURN.
  ENDIF.

ENDFUNCTION.
